allprojects {
    version = "0.9"

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'nz.bradcampbell'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    javadoc {
        failOnError = false
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    buildscript {
        repositories {
            jcenter()
        }

        dependencies {
            classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
        }
    }
}

ext.deps = [
    // Square
    javapoet: 'com.squareup:javapoet:1.4.0',

    // Auto
    autoservice: 'com.google.auto.service:auto-service:1.0-rc2',

    // Test dependencies
    junit: 'junit:junit:4.12',
    truth: 'com.google.truth:truth:0.27',
    compiletesting: 'com.google.testing.compile:compile-testing:0.7',
]
